apply plugin: 'kotlin-multiplatform'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.android.library'

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildToolsVersion = '29.0.2'

    sourceSets {
        getByName("main") {
            manifest.srcFile("src/androidMain/AndroidManifest.xml")
            java.srcDirs("src/androidMain/kotlin")
            res.srcDirs("src/androidMain/res")
        }
    }
}

kotlin {
    android {
        publishLibraryVariants("release", "debug")
    }

    final def buildForDevice = project.findProperty("device")?.toBoolean() ?: false

    targets {
        final def iOSTarget = buildForDevice ? presets.iosArm64 : presets.iosX64

        fromPreset(iOSTarget, 'iOS') {
            binaries {
                framework {
                    baseName = "kuiks"
                    if (!buildForDevice) {
                        embedBitcode("disable")
                    }
                }
            }
            compilations.main {
                cinterops {
                    xctest {
                        // xctest.def require Xcode or symlink to be at /Applications/Xcode.app
                        defFile project.file("src/iosMain/xctest.def")
                    }
                }
            }
        }

        fromPreset(presets.android, 'android')
    }

    sourceSets {
        commonMain.dependencies {
            implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
        }

        androidMain.dependencies {
            implementation 'org.jetbrains.kotlin:kotlin-stdlib'

            implementation 'androidx.test.espresso:espresso-core:3.2.0'
            implementation 'androidx.test.espresso:espresso-contrib:3.2.0'
        }

        iOSMain.dependencies {
        }
    }
}